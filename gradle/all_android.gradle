
android {
    namespace "${project.group ?: 'com.anysoftkeyboard'}.${project.name.replace(':', '.')}"
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
    }

    lint {
        lintConfig = rootProject.file('configs/lint.xml')
        abortOnError false
        checkReleaseBuilds false
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    showCauses true
                    showExceptions true
                    showStackTraces true
                }
                systemProperty 'java.awt.headless', true
                systemProperty 'robolectric.logging', 'stdout'
                systemProperty 'robolectric.logging.enabled', false
                systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
            }
        }
    }
}

apply from: "$rootDir/gradle/versioning_add_on.gradle"

dependencies {
    if (path != ':api') {
        if (plugins.hasPlugin('com.android.library')) {
            api project(path: ':api')
        } else {
            implementation project(path: ':api')
        }
    }

    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'junit:junit:4.13.2'
}

//this should only be applied on the root project
if (path != rootProject.path) {
    throw new IllegalStateException("versioning script should only be applied on the root project")
}

def avPluginId = 'net.evendanan.autoversion.simple'
def useAutoVersioning = true
try {
    apply plugin: avPluginId
} catch (Throwable t) {
    logger.lifecycle("AutoVersioning plugin not available; using fallback version data. ${t.class.simpleName}: ${t.message}")
    useAutoVersioning = false
}

if (useAutoVersioning) {
    autoVersioning {
        enabled = rootProject.hasProperty("withAutoVersioning")
        buildCounterEnvKey = "CIRCLE_BUILD_NUM"
        major = 4
        minor = 0
        buildCounterOffset = 1000
        patchOffset = -1600
    }

    rootProject.afterEvaluate {
        file('.generated_pack_version').write(autoVersioning.versionData.versionName)
    }
} else {
    ext.autoVersioning = [
        versionData: [
            versionCode: 4000000,
            versionName: '4.0.0-local'
        ]
    ]
}
